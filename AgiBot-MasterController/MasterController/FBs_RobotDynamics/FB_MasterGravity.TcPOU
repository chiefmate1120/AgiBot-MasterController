<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_MasterGravity" Id="{ced9fef1-3691-440d-9914-43fe53d631cd}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_MasterGravity
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	// arm index
	m_armIdx :INT;
	
	(*graParam=[ 2.7244*m3 + 2.7244*m4 + 2.7244*m5 + 2.7244*m6 + 9.8*mx2 + 9.8*mx9,
            	9.8*my2 + 9.8*my9,
			 	3.577*m4 + 3.577*m5 + 3.577*m6 + 0.49*m9 + 9.8*mx3 + 9.8*mx8
           		1.5827*m4 + 1.5827*m5 + 1.5827*m6 + 9.8*my3 + 9.8*my8 + 9.8*mz4
            	mz5-my4,	my5+mz6,  	mx4,   	mx5, 	mx6,   	my6
            	preF2,   k2,   preF3,    k3,    preF5   k5]*)
	m_gravityParam :ARRAY[1..16] OF REAL;
	// spring2 [L0, b1, b2, l1, r0]
	m_spring2Param :ARRAY[1..5] OF LREAL;
	// spring3 [L0, b1, b2, l1, r0,d]
	m_spring3Param :ARRAY[1..6] OF LREAL;
	// spring5 [L0, s1, L1, r0, beta]
	m_spring5Param :ARRAY[1..5] OF LREAL;
	
	// gravity deviation
	m_gravityDeviation :Vec7d;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="getRegMat" Id="{2c172cd0-ca8d-4848-b43a-5b6b48878905}">
      <Declaration><![CDATA[METHOD protected getRegMat : BOOL
VAR_INPUT
	// 0 for left, 1 for right
	i_armIdx:INT;
END_VAR
VAR_IN_OUT CONSTANT
	// position of all the active joint
	i_jointPos:ARRAY[1..7] OF LREAL;
END_VAR
VAR_IN_OUT
	o_regMat:ARRAY[1..5,1..16] OF LREAL;
END_VAR
VAR
	q2,q8,q4,q5,q6:LREAL;
	i,j :INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[q2:=i_jointPos[2];
q8:=i_jointPos[3];
q4:=i_jointPos[4];
q5:=i_jointPos[5];
q6:=i_jointPos[6];

FOR i:=1 TO 5 DO
	FOR j:=1 TO 16 DO
		o_regMat[i,j]:=0;
	END_FOR
END_FOR

// active joint 2
o_regMat[1,1]:=COS(q2);
o_regMat[1,2]:=-SIN(q2); 
o_regMat[1,3] := 0;
o_regMat[1,4] :=   0;
o_regMat[1,5] := - 4.9*(COS(q2 - q4) - COS(q4 - q2 + 2.0*q8));
o_regMat[1,6] :=  9.8*COS(q4 - q2 + q8)*SIN(q5)*SIN(q8);
o_regMat[1,7] :=   - 4.9*(SIN(q2 - q4) + SIN(q4 - q2 + 2.0*q8));
o_regMat[1,8] :=   - 9.8*COS(q4 - q2 + q8)*COS(q5)*SIN(q8);
o_regMat[1,9] :=  9.8*SIN(q8)*(SIN(q4 - q2 + q8)*SIN(q6) - COS(q4 - q2 + q8)*COS(q5)*COS(q6));
o_regMat[1,10] :=9.8*SIN(q8)*(SIN(q4 - q2 + q8)*COS(q6) + COS(q4 - q2 + q8)*COS(q5)*SIN(q6));
spring2RegMat(q2, o_preF=>o_regMat[1,11], o_stiff=>o_regMat[1,12]);
o_regMat[1,11]:=-o_regMat[1,11];         
o_regMat[1,12]:=-o_regMat[1,12];

// active joint3
o_regMat[2,3]:= COS(q8);
o_regMat[2,4]:= - SIN(q8);
o_regMat[2,5]:= -9.8*(COS(2.0*q8)*COS(q2)*COS(q4) + COS(2.0*q8)*SIN(q2)*SIN(q4) - SIN(2.0*q8)*COS(q2)*SIN(q4) + SIN(2.0*q8)*COS(q4)*SIN(q2));
o_regMat[2,6]:= -9.8*(COS(q5)*SIN(q8) + COS(2.0*q8)*COS(q2)*SIN(q4)*SIN(q5) - COS(2.0*q8)*COS(q4)*SIN(q2)*SIN(q5) + SIN(2.0*q8)*COS(q2)*COS(q4)*SIN(q5) + SIN(2.0*q8)*SIN(q2)*SIN(q4)*SIN(q5));
o_regMat[2,7]:= 9.8*(COS(2.0*q8)*COS(q2)*SIN(q4) - COS(2.0*q8)*COS(q4)*SIN(q2) + SIN(2.0*q8)*COS(q2)*COS(q4) + SIN(2.0*q8)*SIN(q2)*SIN(q4));
o_regMat[2,8]:= 9.8*(COS(2.0*q8)*COS(q2)*COS(q5)*SIN(q4) - SIN(q5)*SIN(q8) - COS(2.0*q8)*COS(q4)*COS(q5)*SIN(q2) + SIN(2.0*q8)*COS(q2)*COS(q4)*COS(q5) + SIN(2.0*q8)*COS(q5)*SIN(q2)*SIN(q4));
o_regMat[2,9]:= 9.8*(COS(2.0*q8)*COS(q2)*COS(q4)*SIN(q6) - COS(q6)*SIN(q5)*SIN(q8) + COS(2.0*q8)*SIN(q2)*SIN(q4)*SIN(q6) - SIN(2.0*q8)*COS(q2)*SIN(q4)*SIN(q6) + SIN(2.0*q8)*COS(q4)*SIN(q2)*SIN(q6) + COS(2.0*q8)*COS(q2)*COS(q5)*COS(q6)*SIN(q4) - COS(2.0*q8)*COS(q4)*COS(q5)*COS(q6)*SIN(q2) + SIN(2.0*q8)*COS(q2)*COS(q4)*COS(q5)*COS(q6) + SIN(2.0*q8)*COS(q5)*COS(q6)*SIN(q2)*SIN(q4)); 
o_regMat[2,10]:= 9.8*(SIN(q5)*SIN(q6)*SIN(q8) + COS(2.0*q8)*COS(q2)*COS(q4)*COS(q6) + COS(2.0*q8)*COS(q6)*SIN(q2)*SIN(q4) - SIN(2.0*q8)*COS(q2)*COS(q6)*SIN(q4) + SIN(2.0*q8)*COS(q4)*COS(q6)*SIN(q2) - COS(2.0*q8)*COS(q2)*COS(q5)*SIN(q4)*SIN(q6) + COS(2.0*q8)*COS(q4)*COS(q5)*SIN(q2)*SIN(q6) - SIN(2.0*q8)*COS(q2)*COS(q4)*COS(q5)*SIN(q6) - SIN(2.0*q8)*COS(q5)*SIN(q2)*SIN(q4)*SIN(q6));
spring3RegMat(q8, o_preF=>o_regMat[2,13], o_stiff=>o_regMat[2,14]);
o_regMat[2,13]:=-o_regMat[2,13];
o_regMat[2,14]:=-o_regMat[2,14];

// active joint4
o_regMat[3,5]:= 9.8*SIN(q4 - q2 + q8)*SIN(q8);
o_regMat[3,6]:= - 9.8*COS(q4 - q2 + q8)*SIN(q5)*SIN(q8);
o_regMat[3,7]:= 9.8*COS(q4 - q2 + q8)*SIN(q8);
o_regMat[3,8]:= 9.8*COS(q4 - q2 + q8)*COS(q5)*SIN(q8);
o_regMat[3,9]:= - 9.8 *SIN(q8)*(SIN(q4 - q2 + q8)*SIN(q6) - COS(q4 - q2 + q8)*COS(q5)*COS(q6));
o_regMat[3,10]:= - 9.8*SIN(q8)*(SIN(q4 - q2 + q8)*COS(q6) + COS(q4 - q2 + q8)*COS(q5)*SIN(q6));
              
// active joint5
o_regMat[4,6]:= -9.8* (COS(q8)*SIN(q5) + SIN(q4 - q2 + q8)*COS(q5)*SIN(q8));
o_regMat[4,7]:= 0;
o_regMat[4,8]:= 9.8* (COS(q5)*COS(q8) - SIN(q4 - q2 + q8)*SIN(q5)*SIN(q8));
o_regMat[4,9]:= 9.8*COS(q6)*(COS(q5)*COS(q8) - SIN(q4 - q2 + q8)*SIN(q5)*SIN(q8));
o_regMat[4,10]:= -9.8*SIN(q6)*(COS(q5)*COS(q8) - SIN(q4 - q2 + q8)*SIN(q5)*SIN(q8));
spring5RegMat(q5, o_preF=>o_regMat[4,15], o_stiff=>o_regMat[4,16]);
o_regMat[4,15]:=-o_regMat[4,15];
o_regMat[4,16]:=-o_regMat[4,16];

// active joint5
o_regMat[5,9]:= -9.8*(COS(q8)*SIN(q5)*SIN(q6) - COS(q4 - q2 + q8)*COS(q6)*SIN(q8) + SIN(q4 - q2 + q8)*COS(q5)*SIN(q6)*SIN(q8));
o_regMat[5,10]:= -9.8*(COS(q4 - q2 + q8)*SIN(q6)*SIN(q8) + COS(q6)*COS(q8)*SIN(q5) + SIN(q4 - q2 + q8)*COS(q5)*COS(q6)*SIN(q8)); ]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{e7795345-42d8-4dbc-8eda-5e489420b6e1}">
      <Declaration><![CDATA[METHOD PUBLIC init : BOOL
VAR_INPUT
	i_armIdx :INT;
END_VAR
VAR
	i:INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[m_armIdx:=i_armIdx;

FOR i:=1 TO 16 DO
	m_gravityParam[i]:=GVL_ArmDynamics.g_gravityParam[m_armIdx, i];
END_FOR

FOR i:=1 TO 5 DO
	m_spring2Param[i]:=GVL_ArmDynamics.g_spring2Param[i];
	m_spring3Param[i]:=GVL_ArmDynamics.g_spring3Param[i];
	m_spring5Param[i]:=GVL_ArmDynamics.g_spring5Param[i];
END_FOR
m_spring3Param[6]:=GVL_ArmDynamics.g_spring3Param[6];

FOR i:=1 TO 7 DO
	m_gravityDeviation[i]:=GVL_ArmDynamics.g_gravityDeviaion[m_armIdx, i];
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="run" Id="{469433a3-9614-4c99-9efb-b9558878df36}">
      <Declaration><![CDATA[METHOD PUBLIC run : BOOL
VAR_IN_OUT CONSTANT
	// active joint position
	i_jntPos :vec7d;
END_VAR
VAR_IN_OUT
	r_gravity :Vec7d;
END_VAR
VAR
	// gravity regression matrix
	regMat:ARRAY[1..5,1..16] OF LREAL;
	
	i,j:INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[getRegMat(m_armIdx,i_jntPos,regMat);

FOR i:=2 TO 6 DO
	r_gravity[i]:=0;
	FOR j:=1 TO 16 DO
		r_gravity[i]:=r_gravity[i]+regMat[i-1,j]*m_gravityParam[j];
	END_FOR
	
	// add bias torque
	r_gravity[i]:= r_gravity[i] + m_gravityDeviation[i];
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="spring2RegMat" Id="{5b109821-080b-49b3-9ba1-16a251bb3f0c}">
      <Declaration><![CDATA[(*spring force F = preF+k(L-L0)
% torque trq=F*t=preF*t+k*t*(L-L0)=[t, t*(L-L0)]*[preF,k]'
% this function return [t, t(L-L0)]*)
METHOD PROTECTED spring2RegMat : BOOL
VAR_INPUT
	i_q2:LREAL;
END_VAR

VAR
	// spring paramters
	L0, b1, b2, l1, r0 : LREAL;

	theta,length,t:LREAL;  
	
END_VAR

VAR_OUTPUT
	o_preF:LREAL;
	o_stiff:LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[L0:=m_spring2Param[1]; 
b1:=m_spring2Param[2]; 
b2:=m_spring2Param[3]; 
l1:=m_spring2Param[4]; 
r0:=m_spring2Param[5]; 

L0:=L0+b1+b2;
 
//spring length
theta:=i_q2+90*g_deg2Rad;
length:=SQRT(l1*l1+r0*r0+2*l1*r0*COS(theta));

//force arm
t:=r0*l1*SIN(theta)/Length;

//torque direction is determined by theta(or t)
o_preF:=t;
o_stiff:=t*(length-L0);]]></ST>
      </Implementation>
    </Method>
    <Method Name="spring3RegMat" Id="{1ea90450-7234-457b-b1ba-f3b38c13c9cb}">
      <Declaration><![CDATA[(*spring force F = preF+k(L-L0)
% torque trq=F*t=preF*t+k*t*(L-L0)=[t, t*(L-L0)]*[preF,k]'
% this function return [t, t(L-L0)]*)
METHOD PROTECTED spring3RegMat : BOOL
VAR_INPUT
	i_q3:LREAL;
END_VAR

VAR
	// spring paramters
	L0, b1, b2, l1, r0, d: LREAL;

	alpha, beta,tanBeta, length,t:LREAL;  
	
	spring: INT;
END_VAR

VAR_OUTPUT
	o_preF:LREAL;
	o_stiff:LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[L0:=m_spring3Param[1]; 
b1:=m_spring3Param[2]; 
b2:=m_spring3Param[3]; 
l1:=m_spring3Param[4]; 
r0:=m_spring3Param[5];
d:=m_spring3Param[6];

L0:=L0+b1+b2;
 
// spring length
    alpha:=i_q3+90*g_deg2Rad;
    tanBeta:=(r0*SIN(alpha)+d)/(l1+r0*COS(alpha));
    beta:=ATAN(tanBeta);
    length:=d/SIN(beta)+r0*SIN(alpha)/SIN(beta);
    
// force arm
    t:=(l1-d*cot(beta))*SIN(beta);
    
// torque direction is determined by theta(or t)
    o_preF:=t;
    o_stiff:=t*(length-L0);
	
]]></ST>
      </Implementation>
    </Method>
    <Method Name="spring5RegMat" Id="{05ac6ef2-f929-4ed9-8a25-66eefec0593a}">
      <Declaration><![CDATA[(*spring force F = preF+k(L-L0)
 torque trq=F*t=preF*t+k*t*(L-L0)=[t, t*(L-L0)]*[preF,k]'
 this function return [t, t(L-L0)]
 armIdx, 0 for left arm , 1 for right arm]*)
METHOD PROTECTED spring5RegMat : BOOL
VAR_INPUT
	i_q5:LREAL;
END_VAR

VAR
	// spring paramters
	L0, s1, L1, r0, beta: LREAL;

	theta, length,t:LREAL;  
	
	spring: INT;
	joint: INT;
	armIdx: INT;
	b1: LREAL;
END_VAR

VAR_OUTPUT
	o_preF:LREAL;
	o_stiff:LREAL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[L0:=m_spring5Param[1]; 
s1:=m_spring5Param[2]; 
L1:=m_spring5Param[3]; 
r0:=m_spring5Param[4]; 
beta:=m_spring5Param[5];

L0:=L0+s1;
 
// joint 5 motion angle
IF 1=m_armIdx THEN 
	theta:=i_q5-pi;// left arm
ELSE
	theta:=pi-i_q5; // right arm
END_IF

// spring length and force arm
length:=SQRT(L1*L1+r0*r0+2*L1*r0*COS(theta+beta));
t:=L1*r0*SIN(theta+beta)/length;

// torque direction is determined by t
IF 1=m_armIdx THEN 
	o_preF:=t;
	o_stiff:=t*(length-L0);
else
	o_preF:=-t;
	o_stiff:=-t*(length-L0);
END_IF
	
	
]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_MasterGravity">
      <LineId Id="75" Count="0" />
    </LineIds>
    <LineIds Name="FB_MasterGravity.getRegMat">
      <LineId Id="5" Count="0" />
      <LineId Id="15" Count="3" />
      <LineId Id="81" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="82" Count="0" />
      <LineId Id="84" Count="1" />
      <LineId Id="83" Count="0" />
      <LineId Id="125" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="27" Count="7" />
      <LineId Id="86" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="37" Count="0" />
      <LineId Id="126" Count="0" />
      <LineId Id="105" Count="0" />
      <LineId Id="38" Count="8" />
      <LineId Id="49" Count="0" />
      <LineId Id="106" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="107" Count="0" />
      <LineId Id="50" Count="6" />
      <LineId Id="61" Count="5" />
      <LineId Id="87" Count="0" />
      <LineId Id="67" Count="1" />
      <LineId Id="128" Count="0" />
      <LineId Id="74" Count="1" />
      <LineId Id="21" Count="0" />
    </LineIds>
    <LineIds Name="FB_MasterGravity.init">
      <LineId Id="18" Count="1" />
      <LineId Id="5" Count="0" />
      <LineId Id="9" Count="1" />
      <LineId Id="12" Count="4" />
      <LineId Id="11" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="27" Count="2" />
      <LineId Id="26" Count="0" />
    </LineIds>
    <LineIds Name="FB_MasterGravity.run">
      <LineId Id="55" Count="0" />
      <LineId Id="57" Count="1" />
      <LineId Id="82" Count="0" />
      <LineId Id="59" Count="1" />
      <LineId Id="62" Count="0" />
      <LineId Id="120" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_MasterGravity.spring2RegMat">
      <LineId Id="5" Count="0" />
      <LineId Id="58" Count="3" />
      <LineId Id="57" Count="0" />
      <LineId Id="55" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="23" Count="8" />
      <LineId Id="22" Count="0" />
    </LineIds>
    <LineIds Name="FB_MasterGravity.spring3RegMat">
      <LineId Id="105" Count="3" />
      <LineId Id="103" Count="0" />
      <LineId Id="110" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="64" Count="8" />
      <LineId Id="77" Count="0" />
      <LineId Id="62" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="22" Count="0" />
    </LineIds>
    <LineIds Name="FB_MasterGravity.spring5RegMat">
      <LineId Id="163" Count="3" />
      <LineId Id="162" Count="0" />
      <LineId Id="168" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="112" Count="17" />
      <LineId Id="111" Count="0" />
      <LineId Id="132" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="22" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>