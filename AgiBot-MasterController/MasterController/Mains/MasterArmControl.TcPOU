<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="MasterArmControl" Id="{6192c11b-cd6e-4883-b627-958acfb9caf3}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MasterArmControl
VAR
	// slave robot data, input from slave controller box
	m_slaveRobotData AT %I* : MSCommu_slaveStatus;
		
	// master robot data, ouput to slave controller box
	m_masterRobotData AT %Q* :MSCommu_MasterStatus;
	
	// flag to init robot
	m_robotInit :BOOL :=FALSE;
	
	// wait time before robot init  for ethercat to be statble 
	m_robotInitWaitTime : LREAL:=0;
	
	// master robot
	m_masterRobot:FB_MasterRobot;
	
	// master robot information, including arm status and cartesian data
	m_masterStatus AT %Q* :ST_MasterStatus;
	
	// master robot joint data
	m_masterJointsData AT %Q* :ST_MasterJointData;
	
	// all data from other tasks and controller 
	m_masterInputDataPool : ST_MasterInputDataPool;
	
	// code execute time measure
	m_codeTime : Profiler;
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[m_codeTime(START:=TRUE, RESET:=TRUE);

// wait communication to be stable
IF(m_robotInitWaitTime<g_robotInitWaitTime) THEN
	m_robotInitWaitTime := m_robotInitWaitTime+g_armCtrlCycleTime;
	RETURN;
END_IF

// robot init
IF m_robotInit =FALSE THEN
	m_masterRobot.Init();
	m_robotInit:=TRUE;
END_IF

// slave input data cast
slaveInputDataCast(m_slaveRobotData, m_masterInputDataPool.m_slaveStatus);

// robot run
m_masterRobot.run(m_masterInputDataPool);

// update data pool
m_masterRobot.getStatus(m_masterStatus,m_masterJointsData);

// master output data cast
masterOutputDataCast(shoulderPose:=m_masterRobot.shoulderPose, m_masterStatus, m_masterRobotData);

m_codeTime(START:=FALSE);]]></ST>
    </Implementation>
    <Method Name="masterOutputDataCast" Id="{688f8323-8b5a-4cc7-b4b2-cb5f74424c3f}">
      <Declaration><![CDATA[// convert from local ST_MasterStatus for control to MSCommu_MasterStatus for master-slave communication
METHOD masterOutputDataCast : BOOL
VAR_INPUT
	shoulderPose :ARRAY[1..2] OF ST_Frame;
END_VAR
VAR_IN_OUT
	masterStatus :ST_MasterStatus;
	masterOutputData:MSCommu_MasterStatus;
END_VAR

VAR
	i,j : INT;
	poseInMonitor:ST_Frame;
	velInShoulder, wVelInShoulder :Vec3d;
	velInMonitor, wVelInMonitor :Vec3d;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[masterOutputData.m_motionScale :=masterStatus.m_motionScale;

FOR i:=1 TO g_mArmNum DO
	masterOutputData.m_armState[i] := masterStatus.m_armState[i];
	masterOutputData.m_teleSlaveArmIdx[i] :=masterStatus.m_teleSlaveArmIdx[i];
	masterOutputData.m_gripAngle[i] := masterStatus.m_gripAngle[i] ;
	
	poseInMonitor := transform(shoulderPose[i],  masterStatus.m_curArmPose[i]);
	masterOutputData.m_curArmPose[i].m_pos := poseInMonitor.m_pos ;
	masterOutputData.m_curArmPose[i].m_rot := poseInMonitor.m_rot ;
	
	FOR j:=1 TO 3 DO
		velInShoulder[j]:=masterStatus.m_curArmTwist[i][j];
		wVelInShoulder[j]:=masterStatus.m_curArmTwist[i][j+3];
	END_FOR
	matMultiVec(shoulderPose[i].m_rot, velInShoulder,velInMonitor);
	matMultiVec(shoulderPose[i].m_rot, wVelInShoulder,wVelInMonitor);
	
	FOR j:=1 TO 3 DO
		masterOutputData.m_curArmTwist[i][j]:=velInMonitor[j];
		masterOutputData.m_curArmTwist[i][j+3]:=wVelInMonitor[j];
	END_FOR
END_FOR
]]></ST>
      </Implementation>
    </Method>
    <Method Name="slaveInputDataCast" Id="{110717ec-abfc-4dc8-a3ed-d3c3fbcf321b}">
      <Declaration><![CDATA[// convert from MSCommu_Slavetatus for master-slave communication to local ST_SlaveStatus for control
METHOD slaveInputDataCast : BOOL
VAR_IN_OUT
	slaveInputData : MSCommu_SlaveStatus; 
	slaveStatus :ST_SlaveStatus;
END_VAR

VAR
	i :INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[slaveStatus.m_endoscopeMounted :=slaveInputData.m_endoscopeMounted;
slaveStatus.m_endoscopePose.m_pos :=slaveInputData.m_endoscopePose.m_pos;
slaveStatus.m_endoscopePose.m_rot :=slaveInputData.m_endoscopePose.m_rot;
FOR i:= 1 TO g_sArmNum DO
	slaveStatus.m_armState[i] := slaveInputData.m_armState[i];
	slaveStatus.m_instruStauts[i].m_fingerAngle := slaveInputData.m_instruStauts[i].m_fingerAngle;
	slaveStatus.m_instruStauts[i].m_ready := slaveInputData.m_instruStauts[i].m_ready;
	slaveStatus.m_instruStauts[i].m_type := slaveInputData.m_instruStauts[i].m_type;
	slaveStatus.m_curArmPose[i].m_pos :=slaveInputData.m_curArmPose[i].m_pos;
	slaveStatus.m_curArmPose[i].m_rot :=slaveInputData.m_curArmPose[i].m_rot;
	slaveStatus.m_masterSlavePoseError[i] := slaveInputData.m_masterSlavePoseError[i];
END_FOR



]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="MasterArmControl">
      <LineId Id="85" Count="0" />
      <LineId Id="117" Count="0" />
      <LineId Id="112" Count="1" />
      <LineId Id="141" Count="0" />
      <LineId Id="114" Count="2" />
      <LineId Id="62" Count="0" />
      <LineId Id="2" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="172" Count="1" />
      <LineId Id="61" Count="0" />
      <LineId Id="174" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="64" Count="0" />
      <LineId Id="175" Count="1" />
      <LineId Id="88" Count="0" />
      <LineId Id="177" Count="0" />
      <LineId Id="87" Count="0" />
    </LineIds>
    <LineIds Name="MasterArmControl.masterOutputDataCast">
      <LineId Id="5" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="26" Count="2" />
      <LineId Id="46" Count="1" />
      <LineId Id="30" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="48" Count="0" />
      <LineId Id="52" Count="1" />
      <LineId Id="55" Count="0" />
      <LineId Id="54" Count="0" />
      <LineId Id="49" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="56" Count="0" />
      <LineId Id="59" Count="2" />
      <LineId Id="58" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="17" Count="0" />
    </LineIds>
    <LineIds Name="MasterArmControl.slaveInputDataCast">
      <LineId Id="34" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="26" Count="1" />
      <LineId Id="36" Count="1" />
      <LineId Id="31" Count="0" />
      <LineId Id="39" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="8" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>